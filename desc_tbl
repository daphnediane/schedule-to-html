#!/usr/bin/perl

use common::sense;
use utf8;
use Text::CSV;
use Date::Parse qw{ str2time };
use POSIX qw{ strftime };
use HTML::Tiny qw{};

sub splitline {
    my ( $fh ) = @_;
    my $line = <$fh>;
    return unless defined $line;
    my $full_line = $line;
    my @raw = split m{\t}xms, $line;
    my @res;
    while ( @raw ) {
        my $piece = shift @raw;
        if ( $piece =~ m{ \A " }xms ) {
            my $full = $piece;
            while ( $full
                !~ m{ \A " (?: [^"]++ | (?: "" )++ )* " (?:\n\r?+|\r\n?+)?+ \z }xms
                )
            {
                unless ( @raw ) {
                    chomp $full_line;
                    die "Unable to process: [", $full, "]\nin line: ",
                        $full_line, qq{\n};
                }
                $full .= shift @raw;
            } ## end while ( $full !~ ...)
            $full =~ s{ \A " }{}xms;
            $full =~ s{ (?:\n\r+?|\r\n+?)?+ \z }{}xms;
            $full =~ s{ " \z }{}xms;
            $full =~ s{ "" }{"}xmsg;
            push @res, $full;
        } ## end if ( $piece =~ m{ \A " }xms)
        else {
            push @res, $piece;
        }
    } ## end while ( @raw )
    return \@res;
} ## end sub splitline

my $fname = shift;
my $oname = shift // q{-};

open my $fh, "<:encoding(utf16)", $fname
    or die qq{Unable to read: $fname\n};

my $header = splitline( $fh )
    or die "Missing header in: ", $fname, "\n";

sub time_split {
    my ( $time ) = @_;
    return unless defined $time;
    if ( $time =~ s{ \s ([AP]M) \z}{}xms ) {
        return $time, $1;
    }
    else {
        return $time;
    }
} ## end sub time_split

my %schedule;
my %rooms;
my %splits;
my %day_rows;

sub add_time {
    my ( $time ) = @_;
    return if $schedule{ $time };
    $schedule{ $time } = 1;
    my ( $sec, $min, $hour, $mday, $mon, $year, $wday, $yday, $isdst )
        = localtime $time;
    $day_rows{ $yday } += 1;
} ## end sub add_time

my %ids;

while ( my $raw = splitline( $fh ) ) {
    my %record;
    foreach my $index ( keys @{ $raw } ) {
        my $header_text = $header->[ $index ];
        my $header_alt  = $header_text;
        $header_alt =~ s{\s+}{_}xmsg;
        $header_alt =~ s{/}{_}xmsg;
        $header_alt =~ s{_+}{_}xmsg;
        my $raw_text = $raw->[ $index ];
        if ( $raw_text =~ m{\s} ) {
            $raw_text =~ s{\A \s*}{}xms;
            $raw_text =~ s{\s* \z}{}xms;
        }
        $raw_text = undef if $raw_text eq q{};
        $record{ $header_text } = $raw_text;
        $record{ $header_alt }  = $raw_text;
    } ## end foreach my $index ( keys @{...})
    next unless defined $record{ Start_Time };
    next unless $record{ Name } =~ m{\S}xms;
    my $start    = str2time( $record{ Start_Time } );
    my $end      = str2time( $record{ End_Time } );
    my $start_12 = strftime q{%I:%M %p}, localtime $start;
    my $end_12   = strftime q{%I:%M %p}, localtime $end;
    my $idx      = $record{ Room_Idx };
    my $id       = $record{ Uniq_ID };
    my $kind     = $id // q{Unknown};
    $kind =~ s{\d+[[:alpha:]]?\z}{}xms;

    if ( $ids{ $id } ) {
        my $indx = ++$ids{ $id };
        $id .= q{Dup} . $indx;
    }
    else {
        $ids{ $id } = 1;
    }

    $record{ Start_Tm }   = $start;
    $record{ End_Tm }     = $end;
    $record{ Start_12 }   = $start_12;
    $record{ End_12 }     = $end_12;
    $record{ Table_Rows } = 0;
    $record{ Class }      = q{Type} . $kind;
    $record{ HRef }       = $id;

    next if $idx eq q{};
    next if $idx > 90;

    if ( $record{ Room } eq q{split} ) {
        next;
    }

    add_time $start;
    add_time $end;

    my $room = $rooms{ $idx } //= {};
    $room->{ $idx } = $idx;
    $room->{ name }  //= $record{ Room };
    $room->{ hotel } //= $record{ Real_Room };
    $room->{ start }->{ $start } = \%record;
    $room->{ end }->{ $end }     = \%record;
} ## end while ( my $raw = splitline...)

$fh->close
    or die qq{Unable to close $fname: $!\n};

my @room_idxs   = sort { $a <=> $b } keys %rooms;
my @start_times = sort { $a <=> $b } keys %schedule;

# Determine number of rows needed pass 1
foreach my $room ( values %rooms ) {
    my $active;
    my $starts = $room->{ start };
    my $ends   = $room->{ end };
    next unless ref $starts;

    foreach my $time ( @start_times ) {
        undef $active if $ends->{ $time };
        my $rec = $starts->{ $time };
        $active = $rec if defined $rec;
        next unless defined $active;
        ++$active->{ Table_Rows };
    } ## end foreach my $time ( @start_times)
} ## end foreach my $room ( values %rooms)

my $h = HTML::Tiny->new( mode => q{html} );

my $ofh;
if ( $oname eq q{-} ) {
    $ofh = \*STDOUT;
}
else {
    open $ofh, ">:encoding(utf8)", $oname
        or die qq{Unable to write: $oname\n};
}

say { $ofh } $h->open( qw{ table }, { class => q{schedule} } );

say { $ofh } qq{\t}, $h->open( qw{ thead } );
say { $ofh } qq{\t}, $h->open( qw{ tr }, { class => q{schedheadrow} } );
say { $ofh } qq{\t\t},
    $h->th( { class => q{schedhead scheddaycolumn} }, q{Day} );
say { $ofh } qq{\t\t},
    $h->th( { class => q{schedhead schedtimecolumn} }, q{Time} );
foreach my $idx ( @room_idxs ) {
    say { $ofh } qq{\t\t},
        $h->th(
        { class => q{schedhead schedloccolumn roomloc} },
        $rooms{ $idx }->{ hotel } . q{<br/>} . $rooms{ $idx }->{ name }
        );
} ## end foreach my $idx ( @room_idxs)
say { $ofh } qq{\t}, $h->close( qw{ tr } );
say { $ofh } qq{\t}, $h->close( qw{ thead } );

my $active_day;

my @desc;

say { $ofh } qq{\t}, $h->open( qw{ tbody } );
foreach my $time ( @start_times ) {
    my @ltime = localtime $time;
    my ( $sec, $min, $hour, $mday, $mon, $year, $wday, $yday, $isdst )
        = @ltime;

    say { $ofh } qq{\t}, $h->open( qw{ tr }, { class => q{schedtimerow} } );

    if ( !defined $active_day || $active_day != $yday ) {
        $active_day = $yday;
        say { $ofh } qq{\t\t},
            $h->th(
            {   class   => q{schedday scheddaycolumn},
                rowspan => $day_rows{ $yday } // 1
            },
            strftime q{%A},
            @ltime
            );
    } ## end if ( !defined $active_day...)
    else {
        say { $ofh } qq{\t\t<!--Day-->};
    }
    say { $ofh } qq{\t\t},
        $h->th(
        { class => q{schedtime schedtimecolumn} },
        strftime q{%I:%M %p}, @ltime
        );

    my @slot;

    foreach my $idx ( @room_idxs ) {
        my $room = $rooms{ $idx };

        if ( $room->{ end }->{ $time } ) {
            delete $room->{ active };
        }

        my $rec = $room->{ start }->{ $time };
        print { $ofh } qq{\t\t<!--}, $room->{ name };
        if ( defined $rec ) {
            $room->{ active } = $rec;
            my $difficulty = $rec->{ Difficulty };
            my $tokens     = $rec->{ Tokens };

            my @classes = ( q{}, $rec->{ Class } );
            if ( $difficulty =~ m{\A\d+\z}xms ) {
                push @classes, q{Difficulty} . $difficulty;
            }
            if ( $tokens eq q{0} ) {
                undef $tokens;
            }
            if ( defined $tokens ) {
                push @classes, q{NeedTokens};
                $tokens =~ s{\A\s*}{}xms;
                $tokens =~ s{\s*\z}{}xms;
                $tokens = q{TBD} unless $tokens =~ m{\A\d+\z}xms;
            } ## end if ( defined $tokens )

            say { $ofh } q{-->},
                $h->open(
                q{td},
                {   id      => $rec->{ HRef } . q{Grid},
                    rowspan => $rec->{ Table_Rows },
                    class   => (
                        join q{ }, qw{ schedloccolumn },
                        map { q{panel} . $_ } @classes
                    )
                }
                );
            say { $ofh } qq{\t\t\t},
                $h->div( { class => q{panelid} }, $rec->{ Uniq_ID } );
            say { $ofh } qq{\t\t\t},
                $h->open( q{a}, { href => q{#} . $rec->{ HRef } } );
            say { $ofh } qq{\t\t\t\t},
                $h->span( { class => q{panelname} }, $rec->{ Name } );
            if ( defined $tokens ) {
                say { $ofh } qq{\t\t\t\t},
                    $h->div(
                    { class => q{paneltokens paneltokens} . $tokens },
                    q{Tokens: } . $tokens
                    );
            } ## end if ( defined $tokens )

            my $panelist = $rec->{ Panelist };
            if ( $panelist =~ m{\S}xms ) {
                say { $ofh } qq{\t\t\t\t},
                    $h->span( { class => q{panelist} }, $rec->{ Panelist } );
            }
            say { $ofh } qq{\t\t\t}, $h->close( q{a} );
            say { $ofh } qq{\t\t},   $h->close( q{td} );

            push @slot,
                qq{\t\t\t}
                . $h->open(
                q{li},
                {   id    => $rec->{ HRef },
                    class => join q{ }, map { q{desc} . $_ } @classes
                }
                );
            push @slot,
                qq{\t\t\t\t}
                . $h->div(
                { class => q{descid} },
                $rec->{ Uniq_ID }
                );
            push @slot,
                qq{\t\t\t\t}
                . $h->a(
                {   href  => q{#} . $rec->{ HRef } . q{Grid},
                    class => q{descname}
                },
                $rec->{ Name }
                );
            if ( defined $tokens ) {
                push @slot,
                    qq{\t\t\t\t}
                    . $h->div(
                    { class => q{descTokens descTokens} . $tokens },
                    q{Required tokens: } . $tokens
                    );
            } ## end if ( defined $tokens )

            push @slot,
                qq{\t\t\t\t}
                . $h->p(
                { class => q{descroom} },
                $room->{ hotel }
                );
            if ( $panelist =~ m{\S}xms ) {
                push @slot,
                    qq{\t\t\t\t}
                    . $h->p(
                    { class => q{descpanelist} },
                    $rec->{ Panelist }
                    );
            } ## end if ( $panelist =~ m{\S}xms)
            push @slot, qq{\t\t\t\t}
                . $h->p( { class => q{descbody} }, $rec->{ Description } );

            if ( defined $rec->{ Note } ) {
                push @slot,
                    qq{\t\t\t\t}
                    . $h->p(
                    { class => q{descnote} },
                    $h->i( $rec->{ Note } )
                    );
            } ## end if ( defined $rec->{ Note...})

            if ( $difficulty =~ m{\A\d+\z}xms ) {
                push @slot,
                    qq{\t\t\t\t}
                    . $h->p(
                    {   class => q{descDifficulty descDifficulty}
                            . $difficulty
                    },
                    q{Difficulty level: } . $difficulty
                    );
            } ## end if ( $difficulty =~ m{\A\d+\z}xms)

            push @slot, qq{\t\t\t} . $h->close( q{li} );
        } ## end if ( defined $rec )
        elsif ( defined $room->{ active } ) {
            $rec = $room->{ active };

            say { $ofh } q{ }, $rec->{ Uniq_ID }, q{ continued-->};
        }
        else {
            say { $ofh } q{-->}, $h->td( { class => q{empty} } );
        }
    } ## end foreach my $idx ( @room_idxs)

    if ( @slot ) {
        push @desc,
              qq{\t}
            . $h->open( q{li}, { class => q{desctime} } )
            . $h->entity_encode( strftime q{%A %I:%M %p}, @ltime );
        push @desc, qq{\t\t} . $h->open( q{ul} );
        chomp @slot;
        push @desc, @slot;
        push @desc, qq{\t\t} . $h->close( q{ul} );
        push @desc, qq{\t} . $h->close( q{li} );
    } ## end if ( @slot )

    say { $ofh } qq{\t}, $h->close( qw{ tr } );
} ## end foreach my $time ( @start_times)

say { $ofh } qq{\t}, $h->close( qw{ tbody } );
say { $ofh } $h->close( qw{ table } );

if ( @desc ) {
    say { $ofh } join qq{\n},
        $h->open( q{ul}, { class => q{descriptions} } ), @desc,
        $h->close( q{ul} );
}

if ( $oname ne q{-} ) {
    $ofh->close
        or die qq{Unable to close $oname: $!\n};
}

1;
