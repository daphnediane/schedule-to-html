#!/usr/bin/perl

use common::sense;
use strict;
use utf8;
use Text::CSV;
use File::Spec;
use Date::Parse qw{ str2time };
use POSIX qw{ strftime };
use HTML::Tiny qw{};
use Readonly;
use Carp qw{verbose};
use Getopt::Long qw{GetOptionsFromArray};
use File::Slurp qw{read_file};

# Global variables
Readonly our $HALF_HOUR => 30 * 60;
Readonly our $CAFE      => q{Café};

# Panel fields
Readonly our $TABLE_DESCRIPTION => q{Description};
Readonly our $TABLE_DIFFICULTY  => q{Difficulty};
Readonly our $TABLE_END_TIME    => q{End_Time};
Readonly our $TABLE_FULL        => q{Full};
Readonly our $TABLE_HOTEL_ROOM  => q{Real_Room};
Readonly our $TABLE_KIND        => q{Kind};
Readonly our $TABLE_NOTE        => q{Note};
Readonly our $TABLE_PANEL_NAME  => q{Name};
Readonly our $TABLE_PANELIST    => q{Panelist};
Readonly our $TABLE_ROOM_INDEX  => q{Room_Idx};
Readonly our $TABLE_ROOM_NAME   => q{Room};
Readonly our $TABLE_START_TIME  => q{Start_Time};
Readonly our $TABLE_TOKENS      => q{Tokens};
Readonly our $TABLE_UNIQUE_ID   => q{Uniq_ID};

# Custom fields
Readonly our $FIELD_BY_END_SECONDS     => q{*BY_END};
Readonly our $FIELD_BY_REGION          => q{*BY_REGION};
Readonly our $FIELD_BY_ROW_SECONDS     => q{*BY_SECONDS};
Readonly our $FIELD_BY_START_SECONDS   => q{*BY_START};
Readonly our $FIELD_CAFE               => q{*CAFE};
Readonly our $FIELD_CSS_SUBCLASSES     => q{*CLASSES};
Readonly our $FIELD_CSS_HREF           => q{*HREF};
Readonly our $FIELD_DAY_BEING_OUTPUT   => q{*ACTIVEDAY};
Readonly our $FIELD_END_TIME_SECONDS   => q{*END_SEC};
Readonly our $FIELD_GUEST_CONFLICT     => q{*GUEST_CONFLICT};
Readonly our $FIELD_GUEST_MAP          => q{*GUEST_MAP};
Readonly our $FIELD_LAST_TIME_OUTPUT   => q{*LASTTIME};
Readonly our $FIELD_REGION_NAME        => q{*NAME};
Readonly our $FIELD_ROOM_INFO          => q{*ROOM_INFO};
Readonly our $FIELD_ROOM_NAME          => q{*NAME};
Readonly our $FIELD_START_TIME_SECONDS => q{*START_SEC};
Readonly our $FIELD_TABLE_ROWS         => q{*NUM_ROWS};

# HTML Elements
Readonly our $HTML_ANCHOR     => q{a};
Readonly our $HTML_BODY       => q{body};
Readonly our $HTML_COLGROUP   => q{colgroup};
Readonly our $HTML_DIV        => q{div};
Readonly our $HTML_HEAD       => q{head};
Readonly our $HTML_HTML       => q{html};
Readonly our $HTML_TABLE      => q{table};
Readonly our $HTML_TABLE_BODY => q{tbody};
Readonly our $HTML_TABLE_DATA => q{td};
Readonly our $HTML_TABLE_FOOT => q{tfoot};
Readonly our $HTML_TABLE_HEAD => q{thead};
Readonly our $HTML_TABLE_ROW  => q{tr};

# HTML Anchors
Readonly our $ANCHOR_FMT_GRID => q{%sGrid};

# CSS Classes
Readonly our $CLASS_DESC_SECTION             => q{descriptions};
Readonly our $CLASS_DESC_TIME_COLUMN         => q{desccol};
Readonly our $CLASS_DESC_TIME_TABLE          => q{desctime};
Readonly our $CLASS_GRID_CELL_DAY            => q{schedday};
Readonly our $CLASS_GRID_CELL_GUEST_BUSY     => q{schedtimeguest};
Readonly our $CLASS_GRID_CELL_HEADER         => q{schedhead};
Readonly our $CLASS_GRID_CELL_FMT_SUBCLASS   => q{panel%s};
Readonly our $CLASS_GRID_CELL_ROOM_NAME      => q{roomloc};
Readonly our $CLASS_GRID_COLUMN_DAY          => q{scheddaycolumn};
Readonly our $CLASS_GRID_COLUMN_FMT_ROOM_IDX => q{schedloc%scolumn};
Readonly our $CLASS_GRID_COLUMN_ROOM         => q{schedloccolumn};
Readonly our $CLASS_GRID_COLUMN_TIME         => q{schedtimecolumn};
Readonly our $CLASS_GRID_ROW_GUEST_BUSY      => q{schedtimerowguest};
Readonly our $CLASS_GRID_ROW_HEADER          => q{schedheadrow};
Readonly our $CLASS_GRID_TABLE               => q{schedule};

# CSS Subclasses
Readonly our $SUBCLASS_CONFLICT_PANEL   => q{ConflictGuest};
Readonly our $SUBCLASS_FMT_DIFFICULTY   => q{Difficulty%s};
Readonly our $SUBCLASS_FMT_TYPE         => q{Type%s};
Readonly our $SUBCLASS_FULL             => q{Full};
Readonly our $SUBCLASS_GUEST_PANEL      => q{SelectedGuest};
Readonly our $SUBCLASS_NEED_TOKENS      => q{NeedTokens};
Readonly our $SUBCLASS_PIECE_FULL       => q{fulllabel};
Readonly our $SUBCLASS_PIECE_ID         => q{id};
Readonly our $SUBCLASS_PIECE_NAME       => q{name};
Readonly our $SUBCLASS_PIECE_TOKENS     => q{tokens};
Readonly our $SUBCLASS_PIECE_FMT_TOKENS => q{tokens%s};

# Grid headers
Readonly our $HEADING_DAY  => q{Day};
Readonly our $HEADING_TIME => q{Time};

# Formatting strings
Readonly our $FMT_DAY  => q{%A};
Readonly our $FMT_TIME => q{%I:%M %p};

our $fname;
our $oname;
our $day_column;
our $unified;
our $perday;
our $separate_desc;
our $inline_style;
our $split_guest;

our %guests;
our %schedule;
our %rooms;
our %time_region;

our @room_ids;
our @start_times;
our $ofh;
our $level = 0;
our $h = HTML::Tiny->new( mode => q{html} );

sub splitline {
    my ( $fh ) = @_;
    my $line = $fh->getline;
    return unless defined $line;
    my $full_line = $line;
    my @raw = split m{\t}xms, $line;
    my @res;
    while ( @raw ) {
        my $piece = shift @raw;
        if ( $piece =~ m{ \A " }xms ) {
            my $full = $piece;
            while ( $full
                !~ m{ \A " (?: [^"]++ | (?: "" )++ )* " (?:\n\r?+|\r\n?+)?+ \z }xms
                )
            {
                unless ( @raw ) {
                    chomp $full_line;
                    die
                        qq{Unable to process: [${full}]\nin line: ${full_line}\n};
                }
                $full .= shift @raw;
            } ## end while ( $full !~ ...)
            $full =~ s{ \A " }{}xms;
            $full =~ s{ (?:\n\r?+|\r\n?+)?+ \z }{}xms;
            $full =~ s{ " \z }{}xms;
            $full =~ s{ "" }{"}xmsg;
            push @res, $full;
        } ## end if ( $piece =~ m{ \A " }xms)
        else {
            push @res, $piece;
        }
    } ## end while ( @raw )
    return \@res;
} ## end sub splitline

sub time_split {
    my ( $time ) = @_;
    return unless defined $time;
    if ( $time =~ s{ \s ([AP]M) \z}{}xms ) {
        return $time, $1;
    }
    else {
        return $time;
    }
} ## end sub time_split

sub add_split {
    my ( $time, $name ) = @_;
    my $region = $time_region{ $time } //= {};
    $region->{ $FIELD_REGION_NAME } //= $name;
    $region->{ $FIELD_START_TIME_SECONDS } = $time;
    return $region;
} ## end sub add_split

sub add_time {
    my ( $time ) = @_;
    return if $schedule{ $time };
    $schedule{ $time } = 1;
}

sub santize_hdr {
    my ( $hdr ) = @_;
    $hdr =~ s{\s+}{_}xmsg;
    $hdr =~ s{/}{_}xmsg;
    $hdr =~ s{[().,]}{_}xmsg;
    $hdr =~ s{_+}{_}xmsg;
    $hdr =~ s{\A_}{}xmsg;
    $hdr =~ s{_\z}{}xmsg;
    return $hdr;
} ## end sub santize_hdr

sub read_csv_file {
    open my $fh, q{<:encoding(utf16)}, $fname
        or die qq{Unable to read: ${fname}\n};

    my $header = splitline( $fh )
        or die qq{Missing header in: ${fname}\n};

    my %ids;

    my $in_guest_columns;
    while ( my $raw = splitline( $fh ) ) {
        last unless defined $raw;
        my %panel;
        foreach my $index ( keys @{ $raw } ) {
            my $header_text = $header->[ $index ];
            my $header_alt  = santize_hdr( $header_text );

            undef $in_guest_columns if $header_alt =~ m{\AStaff\z}xmsi;

            my $raw_text = $raw->[ $index ];
            if ( $raw_text =~ m{\s} ) {
                $raw_text =~ s{\A \s*}{}xms;
                $raw_text =~ s{\s* \z}{}xms;
            }
            $raw_text = undef if $raw_text eq q{};
            $panel{ $header_text } = $raw_text;
            $panel{ $header_alt }  = $raw_text;

            if ( $in_guest_columns && defined $raw_text ) {
                my @guests = ( $header_text );
                if ( $header_alt =~ m{\AOther_Guests?\z}xmsi ) {
                    @guests = split m{\s*,\s*}xms, $raw_text;
                }
                foreach my $guest ( @guests ) {
                    $guests{ $guest } = santize_hdr( $guest );
                    $panel{ $FIELD_GUEST_MAP }->{ $guest } = 1;
                }
            } ## end if ( $in_guest_columns...)
            $in_guest_columns = 1 if $header_alt =~ m{\AGuests\z}xmsi;
        } ## end foreach my $index ( keys @{...})
        next unless defined $panel{ $TABLE_START_TIME };
        next unless $panel{ $TABLE_PANEL_NAME } =~ m{\S}xms;
        my $start = str2time( $panel{ $TABLE_START_TIME } );
        my $end
            = defined $panel{ $TABLE_END_TIME }
            ? str2time( $panel{ $TABLE_END_TIME } )
            : $start;
        my $idx  = $panel{ $TABLE_ROOM_INDEX };
        my $id   = $panel{ $TABLE_UNIQUE_ID };
        my $kind = $id // q{Unknown};
        $kind =~ s{\d+[[:alpha:]]?\z}{}xms;

        if ( $ids{ $id } ) {
            my $indx = ++$ids{ $id };
            $id .= q{Dup} . $indx;
        }
        else {
            $ids{ $id } = 1;
        }

        my $room_name  = $panel{ $TABLE_ROOM_NAME };
        my $difficulty = $panel{ $TABLE_DIFFICULTY };
        my $tokens     = $panel{ $TABLE_TOKENS };
        my $full       = $panel{ $TABLE_FULL };
        my @subclasses = sprintf $SUBCLASS_FMT_TYPE, $kind;
        if ( defined $difficulty && $difficulty =~ m{\A\d+\z}xms ) {
            push @subclasses, sprintf $SUBCLASS_FMT_DIFFICULTY, $difficulty;
        }
        if ( defined $tokens && $tokens eq q{0} ) {
            undef $tokens;
        }
        if ( defined $tokens ) {
            push @subclasses, $SUBCLASS_NEED_TOKENS;
            $tokens =~ s{\A\s*}{}xms;
            $tokens =~ s{\s*\z}{}xms;
            $tokens = q{TBD} unless $tokens =~ m{\A\d+\z}xms;
        } ## end if ( defined $tokens )
        if ( defined $full ) {
            if ( $full =~ m{\Anot??}xms || $full eq q{} ) {
                undef $full;
            }
            else {
                push @subclasses, $SUBCLASS_FULL;
            }
        } ## end if ( defined $full )

        if ( $panel{ $TABLE_KIND } eq $CAFE ) {
            $panel{ $FIELD_CAFE } = 1;
        }
        $panel{ $FIELD_START_TIME_SECONDS } = $start;
        $panel{ $FIELD_END_TIME_SECONDS }   = $end;
        $panel{ $FIELD_CSS_SUBCLASSES }     = \@subclasses;
        $panel{ $FIELD_CSS_HREF }           = $id;
        $panel{ $TABLE_TOKENS }             = $tokens;
        $panel{ $TABLE_FULL }               = $full;

        if ( $room_name =~ m{\ASPLIT}xmsi ) {
            next if $unified;
            if ( $perday ) {
                next unless $room_name =~ m{\ASPLITDAY}xmsi;
            }
            add_split $start, $panel{ $TABLE_PANEL_NAME };
            next;
        } ## end if ( $room_name =~ m{\ASPLIT}xmsi)

        next if $idx eq q{};
        next if $idx >= 100;

        add_time $start;
        add_time $end;

        my $room = $rooms{ $idx } //= {};
        $panel{ $FIELD_ROOM_INFO } = $room;
        $room->{ $TABLE_ROOM_INDEX } = $idx;
        $room->{ $FIELD_ROOM_NAME }  //= $room_name;
        $room->{ $TABLE_HOTEL_ROOM } //= $panel{ $TABLE_HOTEL_ROOM };
        $room->{ $FIELD_BY_START_SECONDS }->{ $start } = \%panel;
        $room->{ $FIELD_BY_END_SECONDS }->{ $end }     = \%panel;
    } ## end while ( my $raw = splitline...)

    $fh->close
        or die qq{Unable to close $fname: $!\n};

    @room_ids    = sort { $a <=> $b } keys %rooms;
    @start_times = sort { $a <=> $b } keys %schedule;

} ## end sub read_csv_file

sub add_guest_conflicts {
    my ( $panel, @guests ) = @_;
    my $gmap = $panel->{ $FIELD_GUEST_MAP };
    my $cmap;
    foreach my $guest ( @guests ) {
        next if $gmap->{ $guest };
        $cmap //= $panel->{ $FIELD_GUEST_CONFLICT } //= {};
        next if $cmap->{ $guest };
        $cmap->{ $guest } = 1;
    } ## end foreach my $guest ( @guests)
    return;
} ## end sub add_guest_conflicts

sub make_time_ranges {
    my %time_points = map { $_ => 1 } ( keys %time_region, keys %schedule );
    my @time_points = sort { $a <=> $b } keys %time_points;

    my $active_range;
    my $active_time;
    my %active_panels;
    my $last_time;
    my %empty_times;

    my $process_time = sub {
        my ( $time ) = @_;

        foreach my $room_idx ( @room_ids ) {
            my $room = $rooms{ $room_idx };
            if ( exists $active_panels{ $room_idx } ) {
                my $panel     = $active_panels{ $room_idx };
                my $panel_end = $panel->{ $FIELD_END_TIME_SECONDS };
                if ( $panel_end <= $time ) {
                    delete $active_panels{ $room_idx };
                }
            } ## end if ( exists $active_panels...)
            if (defined(
                    my $new_panel
                        = $room->{ $FIELD_BY_START_SECONDS }->{ $time }
                )
                )
            {
                my $region_info
                    = $new_panel->{ $FIELD_BY_REGION }->{ $active_time }
                    //= {};    # fix visual code formating error */
                $region_info->{ $FIELD_TABLE_ROWS } //= 0;
                $region_info->{ $FIELD_START_TIME_SECONDS } = $time;
                $active_panels{ $room_idx } = $new_panel;
            } ## end if ( defined( my $new_panel...))
        } ## end foreach my $room_idx ( @room_ids)
        my %active_guest;
        foreach my $panel ( values %active_panels ) {
            my $region_info = $panel->{ $FIELD_BY_REGION }->{ $active_time }
                //= {};

            # fix visual code formating error */
            ++$region_info->{ $FIELD_TABLE_ROWS };
            my $gmap = $panel->{ $FIELD_GUEST_MAP };
            if ( defined $gmap ) {
                foreach my $guest ( keys %{ $gmap } ) {
                    $active_guest{ $guest } = 1 if $gmap->{ $guest };
                }
            }
        } ## end foreach my $panel ( values ...)
        if ( %active_panels ) {
            foreach my $empty ( keys %empty_times ) {
                $active_range->{ $FIELD_BY_ROW_SECONDS }->{ $empty }
                    = $empty_times{ $empty };
            }
            my $panels = { %active_panels };
            $panels->{ $FIELD_GUEST_MAP } = \%active_guest;
            if ( %active_guest ) {
                foreach my $panel ( values %active_panels ) {
                    add_guest_conflicts( $panel, keys %active_guest );
                }
            }

            $active_range->{ $FIELD_BY_ROW_SECONDS }->{ $time } = $panels;
            $last_time                                          = $time;
            %empty_times                                        = ();
        } ## end if ( %active_panels )
        elsif ( defined $last_time ) {
            $empty_times{ $time } = { %active_panels };
        }
        return;
    };

    my $process_half_hours = sub {
        my ( $split_time ) = @_;
        return unless defined $last_time;
        for (
            my $time = $last_time + $HALF_HOUR ;
            $time < $split_time ;
            $time += $HALF_HOUR
            )
        {
            $process_time->( $time );
        } ## end for ( my $time = $last_time...)
        return;
    };

    foreach my $split_time ( @time_points ) {
        my $next_range = $time_region{ $split_time };
        if ( !defined $active_range && !defined $next_range ) {
            $next_range = add_split( $split_time, q{Before Convention} );
        }

        if ( %active_panels ) {
            $process_half_hours->( $split_time );
        }
        if ( defined $next_range ) {
            foreach my $panel ( values %active_panels ) {
                my $region_info
                    = $panel->{ $FIELD_BY_REGION }->{ $split_time } //= {};
                $region_info->{ $FIELD_TABLE_ROWS }         = 0;
                $region_info->{ $FIELD_START_TIME_SECONDS } = $split_time;
            } ## end foreach my $panel ( values ...)
            $active_range = $next_range;
            $active_time  = $split_time;
            undef $last_time;
            %empty_times = ();
        } ## end if ( defined $next_range)
        $process_time->( $split_time );
    } ## end foreach my $split_time ( @time_points)
} ## end sub make_time_ranges

sub out_line {
    my ( @content ) = @_;
    my $indent = join q{}, ( qq{\t} x $level );
    my $content = join q{}, @content;
    foreach my $line ( split m{\n+}xms, $content ) {
        $line =~ s{\A\s+}{}xms;
        $line =~ s{\s+\Z}{}xms;
        next if $line eq q{};
        say { $ofh } $indent, $line;
    } ## end foreach my $line ( split m{\n+}xms...)
} ## end sub out_line

sub out_open {
    my ( @content ) = @_;
    out_line $h->open( @content );
    ++$level;
}

sub out_close {
    my ( @content ) = @_;
    --$level;
    out_line $h->close( @content );
}

sub out_class {
    my ( @fields ) = @_;
    return unless @fields;
    my $res = join q{ }, @fields;
    $res =~ s{\s\s+}{ }xms;
    $res =~ s{\A\s}{}xms;
    $res =~ s{\s\z}{}xms;
    return if $res eq q{};
    return class => $res;
} ## end sub out_class

sub has_guest {
    my ( $panels, $guest, @invalid_result ) = @_;
    return @invalid_result unless defined $guest;
    return @invalid_result unless exists $guests{ $guest };
    foreach my $idx ( @room_ids ) {
        my $panel = $panels->{ $idx };
        next unless defined $panel;
        return 1 if $panel->{ $FIELD_GUEST_MAP }->{ $guest };
    }
    return;
} ## end sub has_guest

sub decode_time {
    my ( $time, $field ) = @_;

    my @ltime = localtime $time;
    my $day   = strftime $FMT_DAY, @ltime;
    my $tm    = strftime $FMT_TIME, @ltime;

    if ( defined $field ) {
        my $out_day  = ( $field =~ m{(day|both)}xms );
        my $out_time = ( $field =~ m{(time|hour|both)}xms );
        if ( $out_day ) {
            return $out_time ? $day . q{ } . $tm : $day;
        }
        if ( $out_time ) {
            return $tm;
        }
    } ## end if ( defined $field )
    return ( $day, $tm );
} ## end sub decode_time

sub dump_labels {
    my ( $kind ) = @_;

    if ( $day_column ) {
        out_line $h->th(
            {   out_class(
                    $CLASS_GRID_CELL_HEADER,
                    $kind eq $HTML_TABLE_HEAD ? $CLASS_GRID_COLUMN_DAY : ()
                )
            },
            $HEADING_DAY
        );
    } ## end if ( $day_column )
    out_line $h->th(
        {   out_class(
                $CLASS_GRID_CELL_HEADER,
                $kind eq $HTML_TABLE_HEAD ? $CLASS_GRID_COLUMN_TIME : ()
            )
        },
        $HEADING_TIME
    );
    foreach my $idx ( @room_ids ) {
        my $hotel = $rooms{ $idx }->{ $TABLE_HOTEL_ROOM };
        my $name  = $rooms{ $idx }->{ $FIELD_ROOM_NAME };
        if ( $hotel ne $name ) {
            $name = $hotel . $h->br() . $name;
        }
        out_line $h->th(
            {   out_class(
                    $CLASS_GRID_CELL_HEADER, $CLASS_GRID_COLUMN_ROOM,
                    $CLASS_GRID_CELL_ROOM_NAME,
                    $kind eq $HTML_TABLE_HEAD
                    ? ( sprintf $CLASS_GRID_COLUMN_FMT_ROOM_IDX, $idx )
                    : ()
                )
            },
            $name
        );
    } ## end foreach my $idx ( @room_ids)

} ## end sub dump_labels

sub dump_header {
    my ( $region ) = @_;

    out_open $HTML_TABLE, { out_class( $CLASS_GRID_TABLE ) };

    out_open $HTML_COLGROUP;
    if ( $day_column ) {
        out_line $h->col( { out_class( $CLASS_GRID_COLUMN_DAY ) } );
    }
    out_line $h->col( { out_class( $CLASS_GRID_COLUMN_TIME ) } );
    foreach my $idx ( @room_ids ) {
        out_line $h->col(
            { out_class( sprintf $CLASS_GRID_COLUMN_FMT_ROOM_IDX, $idx ) } );
    }
    out_close $HTML_COLGROUP;

    out_open $HTML_TABLE_HEAD;
    out_open $HTML_TABLE_ROW, { out_class( $CLASS_GRID_ROW_HEADER ) };
    dump_labels $HTML_TABLE_HEAD;
    out_close $HTML_TABLE_ROW;
    out_close $HTML_TABLE_HEAD;

    out_open $HTML_TABLE_BODY;
} ## end sub dump_header

sub dump_room {
    my ( $region, $guest, $time, $idx, $panel ) = @_;

    if ( !defined $panel ) {
        out_line q{<!--}, $rooms{ $idx }->{ $FIELD_ROOM_NAME }, q{-->},
            $h->td( { out_class( qw{ empty } ) } );
        return;
    }

    my $region_info = $panel->{ $FIELD_BY_REGION }
        ->{ $region->{ $FIELD_START_TIME_SECONDS } } // {};
    if ( $region_info->{ $FIELD_START_TIME_SECONDS } ne $time ) {
        out_line q{<!--}, $rooms{ $idx }->{ $FIELD_ROOM_NAME }, q{ },
            $panel->{ $TABLE_UNIQUE_ID }, q{ continued-->};
        return;
    }

    my $difficulty = $panel->{ $TABLE_DIFFICULTY };
    my $name       = $panel->{ $TABLE_PANEL_NAME };
    my $panelist   = $panel->{ $TABLE_PANELIST };
    my $tokens     = $panel->{ $TABLE_TOKENS };
    my $room       = $rooms{ $idx };

    if ( $panel->{ $FIELD_CAFE } ) {
        $panelist = $name;
        $name     = q{Café featuring};
    }

    my @subclasses = ( q{}, @{ $panel->{ $FIELD_CSS_SUBCLASSES } } );
    if ( $panel->{ $FIELD_GUEST_MAP }->{ $guest } ) {
        push @subclasses, $SUBCLASS_GUEST_PANEL;
    }
    if ( $panel->{ $FIELD_GUEST_CONFLICT }->{ $guest } ) {
        push @subclasses, $SUBCLASS_CONFLICT_PANEL;
    }

    out_line q{<!--}, $rooms{ $idx }->{ $FIELD_ROOM_NAME }, q{-->};
    out_open $HTML_TABLE_DATA,
        {
        id => ( sprintf $ANCHOR_FMT_GRID, $panel->{ $FIELD_CSS_HREF } ),
        rowspan => ( $region_info->{ $FIELD_TABLE_ROWS } // 1 ),
        out_class(
            $CLASS_GRID_COLUMN_ROOM,
            map { sprintf $CLASS_GRID_CELL_FMT_SUBCLASS, $_ } @subclasses
        )
        };

    out_line $h->div(
        {   out_class(
                sprintf $CLASS_GRID_CELL_FMT_SUBCLASS, $SUBCLASS_PIECE_ID
            )
        },
        $panel->{ $TABLE_UNIQUE_ID }
    );
    out_open $HTML_ANCHOR, { href => q{#} . $panel->{ $FIELD_CSS_HREF } };

    if ( $panel->{ $TABLE_FULL } ) {
        out_line $h->div(
            {   out_class(
                    sprintf $CLASS_GRID_CELL_FMT_SUBCLASS,
                    $SUBCLASS_PIECE_FULL
                )
            },
            q{Workshop is Full}
        );
    } ## end if ( $panel->{ $TABLE_FULL...})
    out_line $h->span(
        {   out_class(
                sprintf $CLASS_GRID_CELL_FMT_SUBCLASS,
                $SUBCLASS_PIECE_NAME
            )
        },
        $name
    );

    if ( defined $tokens ) {
        out_line $h->div(
            {   out_class(
                    map { sprintf $CLASS_GRID_CELL_FMT_SUBCLASS, $_ } (
                        $SUBCLASS_PIECE_TOKENS,
                        sprintf $SUBCLASS_PIECE_FMT_TOKENS, $tokens
                    )
                )
            },
            q{Tokens: } . $tokens
        );
    } ## end if ( defined $tokens )

    if ( $panelist =~ m{\S}xms ) {
        out_line $h->span(
            { out_class( qw{ panelist } ) },
            $panelist
        );
    } ## end if ( $panelist =~ m{\S}xms)

    out_close $HTML_ANCHOR;
    out_close $HTML_TABLE_DATA;
} ## end sub dump_room

sub dump_row {
    my ( $region, $guest, $time, $panels ) = @_;

    my @time_row_classes = qw{ schedtimerow };
    my @time_classes     = (
        $CLASS_GRID_CELL_HEADER, qw{ schedtime },
        $CLASS_GRID_COLUMN_TIME
    );

    if (   defined $guest
        && exists $guests{ $guest }
        && $panels->{ $FIELD_GUEST_MAP }->{ $guest } )
    {
        push @time_row_classes, $CLASS_GRID_ROW_GUEST_BUSY;
        push @time_classes,     $CLASS_GRID_CELL_GUEST_BUSY;
    } ## end if ( defined $guest &&...)

    out_open $HTML_TABLE_ROW, { out_class( @time_row_classes ) };

    if ( $day_column ) {
        out_line $h->th(
            {   out_class(
                    $CLASS_GRID_CELL_HEADER, $CLASS_GRID_CELL_DAY,
                    $CLASS_GRID_COLUMN_DAY
                )
            },
            decode_time( $time, qw{ day } )
        );
        out_line $h->th(
            { out_class( @time_classes ) },
            decode_time( $time, qw{ time } )
        );
    } ## end if ( $day_column )
    else {
        my ( $day, $tm ) = decode_time( $time );
        if (   $region->{ $FIELD_LAST_TIME_OUTPUT } == $time
            || $region->{ $FIELD_DAY_BEING_OUTPUT } ne $day )
        {
            $tm = $day . $h->br() . $tm;
            $region->{ $FIELD_DAY_BEING_OUTPUT } = $day;
        } ## end if ( $region->{ $FIELD_LAST_TIME_OUTPUT...})
        out_line $h->th(
            { out_class( @time_classes ) },
            $tm
        );
    } ## end else [ if ( $day_column ) ]

    foreach my $idx ( @room_ids ) {
        dump_room( $region, $guest, $time, $idx, $panels->{ $idx } );
    }
    out_close $HTML_TABLE_ROW;
} ## end sub dump_row

sub dump_foot {
    out_close $HTML_TABLE_BODY;
    out_open $HTML_TABLE_FOOT;
    out_open $HTML_TABLE_ROW, { out_class( $CLASS_GRID_ROW_HEADER ) };
    dump_labels $HTML_TABLE_FOOT;
    out_close $HTML_TABLE_ROW;
    out_close $HTML_TABLE_FOOT;
    out_close $HTML_TABLE;
} ## end sub dump_foot

sub dump_region {
    my ( $region, $guest ) = @_;

    dump_header( $region );
    my $rows = $region->{ $FIELD_BY_ROW_SECONDS };
    $region->{ $FIELD_DAY_BEING_OUTPUT } = undef;
    my @times = sort { $a <=> $b } keys %{ $rows };
    $region->{ $FIELD_LAST_TIME_OUTPUT } = $times[ -1 ];
    foreach my $time ( @times ) {
        dump_row( $region, $guest, $time, $rows->{ $time } );
    }
    dump_foot( $region );
} ## end sub dump_region

sub dump_desc_start {
    my ( $region ) = @_;

    out_open $HTML_DIV, { out_class( $CLASS_DESC_SECTION ) };
}

sub dump_desc_end {
    my ( $region ) = @_;

    out_close $HTML_DIV;
}

sub dump_desc_time_start {
    my ( $region, $guest, $exclude_guest, $time ) = @_;

    out_open $HTML_TABLE, { out_class( $CLASS_DESC_TIME_TABLE ) };
    out_open $HTML_COLGROUP;
    out_line $h->col( { out_class( $CLASS_DESC_TIME_COLUMN ) } );
    out_close $HTML_COLGROUP;
    out_line $h->thead(
        { out_class( qw{desctimeheader} ) },
        $h->tr(
            $h->th(
                { out_class( $CLASS_DESC_TIME_COLUMN ) },
                decode_time( $time, qw{ both } )
            )
        )
    );
    out_open $HTML_TABLE_BODY;
} ## end sub dump_desc_time_start

sub dump_desc_time_end {
    my ( $region, $time ) = @_;

    out_close $HTML_TABLE_BODY;
    out_close $HTML_TABLE;
} ## end sub dump_desc_time_end

sub dump_desc_panel {
    my ($region, $guest, $exclude_guest, $need_time_header_ref, $time,
        $panel
    ) = @_;

    return unless $panel->{ $FIELD_START_TIME_SECONDS } == $time;

    my @subclasses = ( q{}, @{ $panel->{ $FIELD_CSS_SUBCLASSES } } );

    if ( defined $guest ) {
        if ( $exclude_guest ) {
            if ( $panel->{ $FIELD_GUEST_CONFLICT }->{ $guest } ) {
                push @subclasses, $SUBCLASS_CONFLICT_PANEL;
            }
        }
        else {
            return unless $panel->{ $FIELD_GUEST_MAP }->{ $guest };
            push @subclasses, $SUBCLASS_GUEST_PANEL;
        }
    } ## end if ( defined $guest )
    if ( $$need_time_header_ref ) {
        dump_desc_time_start( $region, $guest, $exclude_guest, $time );
        undef $$need_time_header_ref;
    }

    my $difficulty = $panel->{ $TABLE_DIFFICULTY };
    my $name       = $panel->{ $TABLE_PANEL_NAME };
    my $panelist   = $panel->{ $TABLE_PANELIST };
    my $tokens     = $panel->{ $TABLE_TOKENS };
    my $room       = $panel->{ $FIELD_ROOM_INFO };

    if ( $panel->{ $FIELD_CAFE } ) {
        $name = q{Cosplay Café Featuring } . $name;
    }

    out_open $HTML_TABLE_ROW, { out_class( qw{desctimebody} ) };
    out_open $HTML_TABLE_DATA,
        {
        id => $panel->{ $FIELD_CSS_HREF },
        out_class( map { q{desc} . $_ } @subclasses )
        };
    out_line $h->div(
        { out_class( qw{ descid } ) },
        $panel->{ $TABLE_UNIQUE_ID }
    );
    out_line $h->a(
        {   href => q{#} . $panel->{ $FIELD_CSS_HREF } . q{Grid},
            out_class( qw{descname} )
        },
        $name
    );

    if ( defined $tokens ) {
        out_line $h->div(
            { out_class( qw{ descTokens }, qq{descTokens{$tokens}} ) },
            q{Required tokens: } . $tokens
        );
    } ## end if ( defined $tokens )
    out_line $h->p(
        { out_class( qw{ descroom } ) },
        $room->{ $TABLE_HOTEL_ROOM }
    );
    if ( $panelist =~ m{\S}xms ) {
        out_line $h->p( { out_class( qw{ descpanelist} ) }, $panelist );
    }

    my $descbody = $panel->{ $TABLE_DESCRIPTION };
    out_line $h->p(
        { out_class( qw{ descbody } ) },
        $panel->{ $TABLE_DESCRIPTION }
    );

    my @note;
    if ( defined $panel->{ $TABLE_NOTE } ) {
        push @note, $h->i( $panel->{ $TABLE_NOTE } );
    }
    if ( defined $panel->{ $TABLE_FULL } ) {
        push @note,
            $h->span(
            { out_class( qw{ descfulllabel } ) },
            q{This workshop is full.}
            );
    } ## end if ( defined $panel->{...})
    if ( @note ) {
        out_line $h->p(
            { out_class( qw{ descnote } ) },
            join q{ }, @note
        );
    } ## end if ( @note )
    if ( defined $difficulty ) {
        out_line $h->p(
            {   out_class(
                    qw{ descDifficulty }, qq{descDifficulty${difficulty}}
                )
            },
            q{Difficulty level: } . $difficulty
        );
    } ## end if ( defined $difficulty)
    out_close $HTML_TABLE_DATA;
    out_close $HTML_TABLE_ROW;
} ## end sub dump_desc_panel

sub dump_desc_region_rows {
    my ( $region, $guest, $exclude_guest ) = @_;
    if ( defined $guest ) {
        undef $guest unless exists $guests{ $guest };
    }
    undef $exclude_guest unless defined $guest;

    my $rows = $region->{ $FIELD_BY_ROW_SECONDS };
    $region->{ $FIELD_DAY_BEING_OUTPUT } = undef;
    my @times = sort { $a <=> $b } keys %{ $rows };
    $region->{ $FIELD_LAST_TIME_OUTPUT } = $times[ -1 ];
DUMP_TIME:
    foreach my $time ( @times ) {
        my $need_time_header = 1;
        my $panels           = $rows->{ $time };
        if ( $exclude_guest ) {
            next if has_guest( $panels, $guest );
        }
        foreach my $idx ( @room_ids ) {
            my $panel = $panels->{ $idx };
            next unless defined $panel;
            dump_desc_panel(
                $region,            $guest, $exclude_guest,
                \$need_time_header, $time,  $panel
            );
        } ## end foreach my $idx ( @room_ids)
        unless ( $need_time_header ) {
            dump_desc_time_end( $region, $time );
        }
    } ## end DUMP_TIME: foreach my $time ( @times )
} ## end sub dump_desc_region_rows

sub dump_desc_region {
    my ( $region, $guest ) = @_;

    dump_desc_start( $region );
    dump_desc_region_rows( $region, $guest );
    dump_desc_end( $region );

    if ( exists $guests{ $guest } ) {
        dump_desc_start( $region );
        dump_desc_region_rows( $region, $guest, 1 );
        dump_desc_end( $region );
    }
} ## end sub dump_desc_region

sub dump_desc_all_regions {
    my ( $guest, $exclude_guest ) = @_;

    dump_desc_start();
    foreach my $region_time ( sort { $a <=> $b } keys %time_region ) {
        my $region = $time_region{ $region_time };
        dump_desc_region_rows( $region, $guest, $exclude_guest );
    }
    dump_desc_end();
} ## end sub dump_desc_all_regions

sub dump_tables {
    my ( $guest ) = @_;
    my $guest_file = $guests{ $guest };
    undef $guest unless defined $guest_file;

    my %time_points = map { $_ => 1 } ( keys %time_region );
    my $ofname = $oname;

    if ( $oname eq q{-} ) {
        $ofname = q{<STDOUT>};
        $ofh    = \*STDOUT;
    }
    else {
        if ( -d $oname ) {
            $ofname = File::Spec->catfile(
                $oname,
                ( $guest_file || q{index} ) . q{.html}
            );
        } ## end if ( -d $oname )
        elsif ( $guest_file ) {
            $ofname = $oname . q{.} . $guest_file . q{.html};
        }
        open $ofh, q{>:encoding(utf8)}, $ofname
            or die qq{Unable to write: ${ofname}\n};
    } ## end else [ if ( $oname eq q{-} ) ]

    say { $ofh } q{<!doctype html>};
    out_open $HTML_HTML;
    out_open $HTML_HEAD;
    out_line $h->meta( { charset => q{UTF-8} } );
    out_line $h->meta(
        { name => q{apple-mobile-web-app-capable}, content => q{yes} } );
    out_line $h->title( q{Cosplay America 2019 Schedule} );
    out_line $h->link(
        {   href =>
                q{https://fonts.googleapis.com/css?family=Nunito+Sans&display=swap},
            rel => q{stylesheet}
        }
    );

    if ( defined $inline_style ) {
        my $lines = read_file(
            $inline_style,
            { chomp => 1, array_ref => 1, err_mode => q{carp} }
        );
        if ( $lines ) {
            out_open qw{style};
            foreach my $line ( @{ $lines } ) {
                out_line $line;
            }
            out_close qw{style};
        } ## end if ( $lines )
        else {
            undef $inline_style;
        }
    } ## end if ( defined $inline_style)
    if ( !defined $inline_style ) {
        out_line $h->link(
            {   href => q{index.css}, rel => q{stylesheet},
                type => q{text/css}
            }
        );
    } ## end if ( !defined $inline_style)
    out_close $HTML_HEAD;
    out_open $HTML_BODY;

    foreach my $region_time ( sort { $a <=> $b } keys %time_region ) {
        my $region = $time_region{ $region_time };
        dump_region( $region, $guest );
        dump_desc_region( $region, $guest ) unless $separate_desc;
    }

    if ( $separate_desc ) {
        dump_desc_all_regions( $guest );
        if ( exists $guests{ $guest } ) {
            dump_desc_all_regions( $guest, 1 );
        }
    } ## end if ( $separate_desc )

    out_close $HTML_BODY;
    out_close $HTML_HTML;

    if ( $oname ne q{-} ) {
        $ofh->close
            or die qq{Unable to close $ofname: $!\n};
    }
    undef $ofh;
} ## end sub dump_tables

sub main {
    my ( @args ) = @_;

    GetOptionsFromArray(
        \@args,
        q{input=s}   => \$fname,
        q{output=s}  => \$oname,
        q{day!}      => \$day_column,
        q{unified!}  => \$unified,
        q{perday!}   => \$perday,
        q{separate!} => \$separate_desc,
        q{style=s}   => \$inline_style,
        q{guests!}   => \$split_guest,
    ) or die qq{Usage: desc_tbl -input [file] -output [file]\n};

    $fname //= shift @args;
    $oname //= shift @args;
    $oname //= q{-};

    read_csv_file( $fname );

    make_time_ranges;

    if ( $split_guest ) {
        foreach my $guest ( keys %guests ) {
            dump_tables( $guest );
        }
    }
    dump_tables;
} ## end sub main

main( @ARGV );

1;
